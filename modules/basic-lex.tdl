;;; -*- Mode: TDL; Package: LKB -*-
;;;
;;; Basic lexical types, just enough to test word
;;; order modules for now.  Will need to build this
;;; out as a set of alternative modules.


noun-lex := basic-noun-lex & basic-one-arg &
  [ SYNSEM.LOCAL [ CAT.VAL [ SPR < #spr >,
			     COMPS < >,
			     SUBJ < >,
			     SPEC < > ]],
    ARG-ST < #spr > ].

transitive-verb-lex := basic-verb-lex & transitive-lex-item &
  [ SYNSEM.LOCAL [ CAT.VAL [ SPR < >,
			     COMPS < #comps >,
			     SUBJ < #subj >,
			     SPEC < > ]],
    ARG-ST < #subj &
	     [ LOCAL.CAT.VAL.SPR < > ], 
	     #comps &
	     [ LOCAL.CAT.VAL.SPR < > ] > ].

intransitive-verb-lex := basic-verb-lex & intransitive-lex-item &
  [ SYNSEM.LOCAL [ CAT.VAL [ SPR < >,
			     COMPS < >,
			     SUBJ < #subj >,
			     SPEC < > ]],
    ARG-ST < #subj &
	     [ LOCAL.CAT.VAL.SPR < > ] > ].

ditransitive-verb-lex := basic-verb-lex & ditransitive-lex-item &
  [ SYNSEM.LOCAL [ CAT.VAL [ SPR < >,
			     COMPS < #comps1, #comps2 >,
			     SUBJ < #subj >,
			     SPEC < > ]],
    ARG-ST < #subj &
	     [ LOCAL.CAT.VAL.SPR < > ], 
	     #comps1 &
	     [ LOCAL.CAT.VAL.SPR < > ],
	     #comps2 &
	     [ LOCAL.CAT.VAL.SPR < > ] > ].
