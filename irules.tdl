; File for spelling preserving lexical rules, i.e., instances
; of (subtypes of) infl-ltol-rule and infl-ltow-rule.

; Every rule in this file should have a %suffix line.  Those
; that don't belong in lrules.tdl.

%(letter-set (!c bdfglmnprstz))
%(letter-set (!s abcdefghijklmnopqrtuvwxyz05))
%(letter-set (!t bcdfghjklmnpqrstvwxz))
%(letter-set (!v aeiou))

plur-noun-infl-rule :=
%suffix (!s !ss) (!ss !ssses) (ss sses) (!ty !ties) (ch ches) (sh shes) (x xes) (z zes)
lex-rule-infl &
[ NEEDS-AFFIX +,
  SYNSEM.LOCAL plur-noun ].

third-sg-fin-verb-infl-rule :=
%suffix (!s !ss) (!ss !ssses) (ss sses) (!ty !ties) (ch ches) (sh shes) (x xes) (z zes)
lex-rule-infl &
[ NEEDS-AFFIX +,
  SYNSEM.LOCAL third-sg-fin-verb ].

psp-verb-infl-rule :=
%suffix (* ed) (!ty !tied) (e ed) (!t!v!c !t!v!c!ced) 
lex-rule-infl &
[ NEEDS-AFFIX +,
  SYNSEM.LOCAL psp-verb ].

past-verb-infl-rule :=
%suffix (* ed) (!ty !tied) (e ed) (!t!v!c !t!v!c!ced) 
lex-rule-infl &
[ NEEDS-AFFIX +,
  SYNSEM.LOCAL past-verb ].

prp-verb-infl-rule :=
%suffix (!t!v!c !t!v!c!cing) (* ing) (e ing) (ee eeing) (ie ying)
lex-rule-infl &
[ NEEDS-AFFIX +,
  SYNSEM.LOCAL prp-verb ].

bse-verb-infl-rule := lex-rule-infl &
[ NEEDS-AFFIX -,
  STEM #stem,
  DTR.STEM #stem,
  SYNSEM.LOCAL bse-verb ].

non-third-sg-fin-verb-infl-rule := lex-rule-infl &
[ NEEDS-AFFIX -,
  STEM #stem,
  DTR.STEM #stem,
  SYNSEM.LOCAL non-third-sg-fin-verb ].

sing-noun-infl-rule := lex-rule-infl &
[ NEEDS-AFFIX -,
  STEM #stem,
  DTR.STEM #stem,
  SYNSEM.LOCAL sing-noun ].


